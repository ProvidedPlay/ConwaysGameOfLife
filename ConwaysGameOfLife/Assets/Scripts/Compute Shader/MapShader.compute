// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel InitializeGameBoard
#pragma kernel UpdateGameBoard
#pragma kernel LoadInputCellsFromCPU
#pragma kernel CompareCurrentCellsBufferToCPUSnapshot
#pragma kernel WriteCurrentCellsBufferTempToCurrentCellsBuffer
#pragma kernel WriteLivingCellsWithinBoundsToOutputBuffer

struct CellData
{
    int2 position;
    int value;
};
// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> MapRenderTexture;
//RWTexture2D<float> AllCellsGrid;
RWStructuredBuffer<uint> AllCellsBufferCPUSnapshot; // stores a snapshot of the game board (when the play button was last hit) as a 1 dimensional buffer, 
RWStructuredBuffer<uint> AllCellsBufferCurrent; // stores the current game board as a 1 dimensional buffer
RWStructuredBuffer<uint> AllCellsBufferCurrentTemp; // a cached version of the changed game board. When GOL ticks, changes are made to this board not the actual AllCellBufferCurrent. Once the full operation is complete, the values in this AllCellsBufferCurrent are set to the values in this cached version
RWStructuredBuffer<CellData> OutputCellsBuffer; //stores cells changed between the current CellsBuffer and the CPU Snapshot CellsBuffer, to be given back to the CPU to update; stores their x,y positions; alternately, stores all output cells when required from cpu
RWStructuredBuffer<uint> OutputCellsCountBuffer; //a single element buffer that counts the number of output cells in OutputCellsBuffer
RWStructuredBuffer<CellData> AllInputCellsBuffer;

float4 defaultColourOff;
float4 defaultColourOn;
//float4 defaultColourDead;

uint gameBoardWidth;
uint gameBoardHeight;

uint2 boundsMin;
uint2 boundsMax;

int numInputCells;

uint GetLivingNeighborCount(uint x, uint y)
{
    uint livingNeighborCount = 0;

    // Check each neighbor (including diagonals)
    for (int i = -1; i <= 1; i++)
    {
        for (int j = -1; j <= 1; j++)
        {
            if (i == 0 && j == 0)
                continue; // Skip the center cell

            uint neighborXAxisPosition = x + i;
            uint neighborYAxisPosition = y + j;

            // Ensure we're within bounds
            if (neighborXAxisPosition < gameBoardWidth && neighborYAxisPosition < gameBoardHeight)//check if the cell is within bounds
            {
                int neighborIndex = neighborYAxisPosition * gameBoardWidth + neighborXAxisPosition;
                uint neighborState = AllCellsBufferCurrent[neighborIndex];
                livingNeighborCount += neighborState; // Count living neighbors
            }
        }
    }
    
    return livingNeighborCount;
}

//Kernel #0, InitializeGameBoard
[numthreads(16,16,1)]
void InitializeGameBoard(uint3 id : SV_DispatchThreadID) //For each pixel in the 2d texture AllCellsGrid (each value of x,y), sets the value of the cell to 0 (aka dead)
{
    
    uint x = id.x;
    uint y = id.y;
    if (x >= gameBoardWidth || y >= gameBoardHeight)//this makes sure that the compute shader doesnt go out of bounds, this is because the last batch of threads may have more threads (ie more x's and y's) than the board has cells.
    {
        return;
    }
    
    int cellIndex = y * gameBoardWidth + x; // flattens two dimensions into a single dimensional cellIndex. Every y value is the y*width's cell number (eg if it has a width of 11, the bottom left corner would be (0*11=0 +1) 1 in the index. The next line up where x=1 will be (1*11=11)+1 the 12th cell. 
    
    //read the current cellstate
    //uint cellstate = AllCellsBuffer[cellIndex];
    
    AllCellsBufferCPUSnapshot[cellIndex] = 0;
    AllCellsBufferCurrent[cellIndex] = 0;
    AllCellsBufferCurrentTemp[cellIndex] = 0;
    
    if (id.x == 0 && id.y == 0)//Only one thread needs to do this
    {
        OutputCellsCountBuffer[0] = 0;
    }
    //LivingCellsBuffer[cellIndex] = 0;
    
    //Initialize the renderTexture as a clear board
    float4 cellColor = defaultColourOff;
    
    MapRenderTexture[id.xy] = cellColor;

}

//Kernel #1 UpdateGameBoard
[numthreads(16,16,1)]
void UpdateGameBoard(uint3 id : SV_DispatchThreadID)
{
    uint x = id.x;
    uint y = id.y;
    if (x >= gameBoardWidth || y >= gameBoardHeight)//this makes sure that the compute shader doesnt go out of bounds, this is because the last batch of threads may have more threads (ie more x's and y's) than the board has cells.
    {
        return;
    }
    
    int cellIndex = y * gameBoardWidth + x;
    
    GroupMemoryBarrierWithGroupSync(); //ensures that all threads in the same group have reached this point before continuing
    
     //read the current cellstate
    uint cellState = AllCellsBufferCurrent[cellIndex];
    
    //Run GOL Code Here
    
    uint numberOfLivingNeighbors = GetLivingNeighborCount(x, y);
    
    uint newCellState = cellState;
    
    if (cellState == 1) // Cell is alive
    {
        // Apply the survival rule: Cell survives if it has 2 or 3 neighbors
        if (numberOfLivingNeighbors < 2 || numberOfLivingNeighbors > 3)
        {
            newCellState = 0; // Cell dies due to underpopulation/overpopulation
        }
    }
    else // Cell is dead
    {
        // Apply the birth rule: Cell becomes alive if it has exactly 3 neighbors
        if (numberOfLivingNeighbors ==3)
        {
            newCellState = 1; // Cell becomes alive
        }
    }
    AllCellsBufferCurrentTemp[cellIndex] = newCellState;
}

[numthreads(256, 1, 1)]
void LoadInputCellsFromCPU(uint3 id : SV_DispatchThreadID)
{
    int i = id.x; 
    if (i >= numInputCells)
    {
        return;
    }
    
    //retrieve the x,y position of the living cell
    int2 inputCellPosition = AllInputCellsBuffer[i].position; 
    
    //flatten the (x,y) position to a 1d index holding both values for each indexed output value, this will be a 1 to 1 relation to the corresponding cell in our AllCellsBuffer
    int inputCellIndex = inputCellPosition.y * gameBoardWidth + inputCellPosition.x;
    
    int inputCellValue = AllInputCellsBuffer[i].value;
    //Set that cell as alive in AllCellsBuffer
    AllCellsBufferCPUSnapshot[inputCellIndex] = inputCellValue;
    AllCellsBufferCurrent[inputCellIndex] = inputCellValue;
    
    //Render the new cells onto the texture
    
    float4 cellColour;
    cellColour = inputCellValue == 1 ? defaultColourOn : defaultColourOff;
    MapRenderTexture[inputCellPosition] = cellColour;
}

[numthreads(256, 1, 1)]
void CompareCurrentCellsBufferToCPUSnapshot(uint3 id : SV_DispatchThreadID)
{
    uint currentCellIndex = id.x;

    if (currentCellIndex >= gameBoardWidth * gameBoardHeight)
    {
        return;
    }

    uint x = currentCellIndex % gameBoardWidth; //the remainder is the cell's xaxis value
    uint y = currentCellIndex / gameBoardWidth; //the value without the remainder is cell's yaxis value
    
    uint currentCellState = AllCellsBufferCurrent[currentCellIndex];
    uint snapshottedCellState = AllCellsBufferCPUSnapshot[currentCellIndex];

    // If the cell has changed, add it to ChangedCellsBuffer
    if (currentCellState != snapshottedCellState)
    {
        uint newChangedCellIndex;
        InterlockedAdd(OutputCellsCountBuffer[0], 1, newChangedCellIndex); // Get the next available index; adds the 1 to the value of the current ChangedCellsCount (which is stored at element 0 of the ChangedCellsCountBuffer), then sets the newChangedCellIndex to that number
        OutputCellsBuffer[newChangedCellIndex].position = int2(x, y); // Store the changed cell's position in the ChangedCellsBuffer, at the index determined by the value of newChangedCellIndex
        OutputCellsBuffer[newChangedCellIndex].value = currentCellState; //store the changed cell's new state
    }
}

[numthreads(16, 16, 1)]
void WriteCurrentCellsBufferTempToCurrentCellsBuffer(uint3 id : SV_DispatchThreadID)
{   
    uint x = id.x;
    uint y = id.y;
    if (x >= gameBoardWidth || y >= gameBoardHeight)
    {
        return;
    }
    
    int cellIndex = y * gameBoardWidth + x;
    
    GroupMemoryBarrierWithGroupSync();
    
     //read the current cellstate and old cellstate
    uint newCellState = AllCellsBufferCurrentTemp[cellIndex];
    
    AllCellsBufferCurrent[cellIndex] = newCellState;
        
    //If the state has changed, update the colour
        
    float4 cellColour;
    cellColour = newCellState == 1 ? defaultColourOn : defaultColourOff;
    MapRenderTexture[id.xy] = cellColour;
}
[numthreads(256,1,1)]
void WriteLivingCellsWithinBoundsToOutputBuffer(uint3 id : SV_DispatchThreadID)
{
    uint currentCellIndex = id.x;
    uint x = currentCellIndex % gameBoardWidth; //the remainder is the cell's xaxis value
    uint y = currentCellIndex / gameBoardWidth; //the value without the remainder is cell's yaxis value

    if (!(currentCellIndex < gameBoardWidth * gameBoardHeight && boundsMin.x <= x && x <= boundsMax.x && boundsMin.y <= y && y <= boundsMax.y))//if the thread ID is not pointing at a cell within the set selection bounds, dont run the kernel
    {
        return;
    }

    uint currentCellState = AllCellsBufferCurrent[currentCellIndex];
    
    // If the cell is alive, add it to OutputCellsBuffer
    if (currentCellState == 1)
    {
        uint newOutputCellIndex;
        InterlockedAdd(OutputCellsCountBuffer[0], 1, newOutputCellIndex); // Get the next available index; adds the 1 to the value of the current OutputCellsCount (which is stored at element 0 of the OutputCellsCountBuffer), then sets the newOutputCellIndex to that number
        OutputCellsBuffer[newOutputCellIndex].position = int2(x, y); // Store the output cell's position in the OutputCellsBuffer, at the index determined by the value of newOutputCellIndex
        OutputCellsBuffer[newOutputCellIndex].value = currentCellState; //Store the Output cell's new state
    }


}

